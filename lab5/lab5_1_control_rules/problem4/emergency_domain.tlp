; Variable abbreviations:
; c - carrier
; l - location
; t - resource type
; u - uav
; d - depot

(clear-world-symbols)			;Remove old domain symbols

(declare-described-symbols
  ;typing
  (predicate uav 1)
  (predicate depot 1)
  (predicate location 1)
  (predicate type 1)
  (predicate carrier 1)

  ;predicates
  (predicate loc 2)
  (predicate empty 1)
  (predicate carrying 2)

  ;functions
  (function boxcount 2)
  (function used 1)
  (function unused 1)
  (function cratesUndelivered 2)
  (function totalCratesUndelivered 1)
  )

(set-tl-control
  (always (and

            ;Only pickup a type if there is some place to deliver it
            (forall (?u) (uav ?u) (?t) (type ?t)
                    (implies (not(exists (?x) (location ?x) (not (= (cratesUndelivered ?t ?x) 0))))
                             (next (not (carrying ?u ?t)))))

            ;Fly directly to destination
            (forall (?u) (uav ?u) (?from) (location ?from) (?to) (location ?to)
                    (implies (and (loc ?u ?from) (next (loc ?u ?to)) (not (= ?from ?to)))
                             (next (next (loc ?u ?to)))))

            ;Take carrier to depot if more than 1 crate is needed
            (forall (?r) (carrier ?r) (?car_loc) (location ?car_loc)
                    (implies
                      (and
                        (exists (?dest) (location ?dest) (?t) (type ?t)
                                (> (cratesUndelivered ?t ?dest) 1)
                                )
                        (loc ?r ?car_loc)
                        (not (exists (?dep) (depot ?dep) (loc ?dep ?car_loc)))
                        )
                      (exists (?dep) (depot ?dep) (?dep_loc) (location ?dep_loc)
                              (and
                                (loc ?dep ?dep_loc)
                                (eventually (loc ?r ?dep_loc))
                                )
                              )
                      ))

            ;Fly to where carried crate is needed
            (forall (?u) (uav ?u) (?from) (location ?from) (?to) (location ?to) (?t) (type ?t)
                    (implies (and (loc ?u ?from) (carrying ?u ?t) (next (loc ?u ?to)) (not (= ?from ?to)))
                             (> (cratesUndelivered ?t ?to) 0)))

            ;Load available carrier if needed
            (forall (?u) (uav ?u) (?c) (carrier ?c) (?dep) (depot ?dep) (?dep_loc) (location ?dep_loc) (?t) (type ?t)
                    (implies
                      (and
                        (loc ?dep ?dep_loc)
                        (loc ?u ?dep_loc)
                        (loc ?c ?dep_loc)
                        (carrying ?u ?t)
                        (exists (?l) (location ?l)
                                  (> (totalCratesUndelivered ?l) 1) ;If a location needs multiple crates
                                  )
                        (not (= (unused ?c) 0)) ;If there is space on the carrier
                        )
                      (and
                        (next (not (carrying ?u ?t)))
                        )
                      ))

            ;Don't load unneeded crates
            ;(There is no carrier loaded with more crates than is needed at any single location)
            (not (exists (?c) (carrier ?c) (?t) (type ?t)
                         (forall (?l) (location ?l)
                                 (> (boxcount ?c ?t) (cratesUndelivered ?t ?l)))))

            ;Pick up carrier if needed
            (forall (?u) (uav ?u) (?c) (carrier ?c) (?from) (location ?from)
                    (implies
                      (and
                        (loc ?u ?from)
                        (loc ?c ?from)
                        (= (totalCratesUndelivered ?from) 0)
                        (exists (?to) (location ?to)
                                (or
                                  (and
                                    (forall (?t) (type ?t)
                                            (and
                                              (<= (boxcount ?c ?t) (cratesUndelivered ?t ?to))
                                              (> (totalCratesUndelivered ?to) 0)
                                              ))
                                    (= (used ?c) 4)
                                    )
                                  (and
                                    (forall (?t) (type ?t)
                                            (and
                                              (= (boxcount ?c ?t) (cratesUndelivered ?t ?to))
                                              (> (cratesUndelivered ?t ?to) 0)
                                              ))
                                    (< (used ?c) 4)
                                    )
                                  )
                                )
                        )
                      (next (carrying ?u ?c))
                      ))

            ;Deliver carrier if needed
            (forall (?u) (uav ?u) (?c) (carrier ?c) (?from) (location ?from)
                    (implies
                      (and
                        (loc ?u ?from)
                        (= (totalCratesUndelivered ?from) 0)
                        (carrying ?u ?c)
                        (exists (?to) (location ?to)
                                (or
                                  (and
                                    (forall (?t) (type ?t) (<= (boxcount ?c ?t) (cratesUndelivered ?t ?to)))
                                    (= (used ?c) 4)
                                    )
                                  (and
                                    (forall (?t) (type ?t) (= (boxcount ?c ?t) (cratesUndelivered ?t ?to)))
                                    (< (used ?c) 4)
                                    )
                                  )
                                )
                        )
                      (exists (?to) (location ?to)
                              (and
                                (or
                                  (and
                                    (forall (?t) (type ?t) (<= (boxcount ?c ?t) (cratesUndelivered ?t ?to)))
                                    (= (used ?c) 4)
                                    )
                                  (and
                                    (forall (?t) (type ?t) (= (boxcount ?c ?t) (cratesUndelivered ?t ?to)))
                                    (< (used ?c) 4)
                                    )
                                  )
                                (next (loc ?u ?to))
                                (next (next (not (carrying ?u ?c))))
                                )
                              )
                      ))

            ;Unload crates from carrier
            (forall (?u) (uav ?u) (?c) (carrier ?c) (?t) (type ?t) (?l) (location ?l)
                    (implies
                      (and
                        (loc ?u ?l)
                        (loc ?c ?l)
                        (not (exists (?t) (type ?t) (carrying ?u ?t)))
                        (> (cratesUndelivered ?t ?l) 0)
                        (> (boxcount ?c ?t) 0)
                        )
                      (and
                        (next (carrying ?u ?t))
                        (next (next (not (carrying ?u ?t))))
                        )
                      ))

            ;Only put down an empty carrier in a depot
            (forall (?u) (uav ?u) (?c) (carrier ?c) (?l) (location ?l)
                    (implies
                      (and
                        (loc ?u ?l)
                        (carrying ?u ?c)
                        (= (used ?c) 0)
                        (not (exists (?dep) (depot ?dep) (loc ?dep ?l)))
                        )
                      (next(carrying ?u ?c))
                    ))


            )))


; Crates are picked up from the depot via this operator.
(def-adl-operator (pick-up-crate ?u ?l ?d ?t)
                  (pre
                    (?u) (uav ?u)
                    (?d) (depot ?d)
                    (?l) (location ?l)
                    (?t) (type ?t)

                    (and 	(loc ?d ?l)
                            (loc ?u ?l)
                            (empty ?u)))

                  (add	(carrying ?u ?t))

                  (del 	(empty ?u))
                  )

;Deliver a crate to a location where it is needed
(def-adl-operator (put-down-crate ?u ?l ?t)
                  (pre
                    (?u) (uav ?u)
                    (?l) (location ?l)
                    (?t) (type ?t)

                    (and 	(loc ?u ?l)
                            (carrying ?u ?t)
                            (not (= (cratesUndelivered ?t ?l) 0))))

                  (add	(-= (cratesUndelivered ?t ?l) 1)
                        (-= (totalCratesUndelivered ?l) 1)
                        (empty ?u))

                  (del 	(carrying ?u ?t))
                  )

;An empty UAV can picks up a carrier
(def-adl-operator (pick-up-carrier ?u ?c ?l)
                  (pre
                    (?u) (uav ?u)
                    (?c) (carrier ?c)
                    (?l) (location ?l)

                    (and 	(loc ?c ?l)
                            (loc ?u ?l)
                            (empty ?u)))

                  (add	(carrying ?u ?c))

                  (del 	(empty ?u) (loc ?c ?l))
                  )

;A UAV holding a carrier can put it down
(def-adl-operator (put-down-carrier ?u ?c ?l)
                  (pre
                    (?u) (uav ?u)
                    (?c) (carrier ?c)
                    (?l) (location ?l)

                    (and 	(carrying ?u ?c)
                            (loc ?u ?l)))

                  (add	(loc ?c ?l)
                        (empty ?u))

                  (del 	(carrying ?u ?c))
                  )

;A UAV holding a crate can put it on a carrier
(def-adl-operator (load-carrier ?u ?c ?l ?t)
                  (pre
                    (?u) (uav ?u)
                    (?c) (carrier ?c)
                    (?l) (location ?l)
                    (?t) (type ?t)

                    (and  	(not (= (unused ?c) 0))
                            (loc ?c ?l)
                            (loc ?u ?l)
                            (carrying ?u ?t)))
                  (add
                    (+= (used ?c) 1)
                    (-= (unused ?c) 1)
                    (+= (boxcount ?c ?t) 1)
                    (empty ?u))

                  (del
                    (carrying ?u ?t))
                  )

;Pick a crate from a carrier
(def-adl-operator (unload-carrier ?u ?c ?l ?t)
                  (pre
                    (?u) (uav ?u)
                    (?c) (carrier ?c)
                    (?l) (location ?l)
                    (?t) (type ?t)

                    (and  	(empty ?u)
                            (loc ?c ?l)
                            (loc ?u ?l)
                            (not (= (boxcount ?c ?t) 0))))
                  (add
                    (-= (used ?c) 1)
                    (+= (unused ?c) 1)
                    (-= (boxcount ?c ?t) 1)
                    (carrying ?u ?t))

                  (del
                    (empty ?u))
                  )

; Fly a UAV
(def-adl-operator (fly-to ?u ?from ?to)
                  (pre
                    (?u)    (uav ?u)
                    (?from) (location ?from)
                    (?to)   (location ?to)

                    (and  	(loc ?u ?from)))

                  (add	(loc ?u ?to))

                  (del 	(loc ?u ?from))
                  )
